-- It will receive ids of lampposts and the desired type of operation (e.g. color and intensity 
-- of light to be set. It will pass the appropriate command to light_ctrl component --
package sslights::processing::lamppost_controller
public
	with sslights::datatypes;
process lamppost_controller 
	features 
		lp_ids : in data port sslights::datatypes::lamppost.i;
		act_type : in data port sslights::datatypes::actuation_type;
		color_cmd : out data port sslights::datatypes::color_cmd;
		intensity_cmd : out data port sslights::datatypes::intensity_cmd;
	flows 
		f00 : flow path lp_ids -> color_cmd;
		f01 : flow path act_type -> color_cmd; 
		
		f10 : flow path lp_ids -> intensity_cmd; 
		f11 : flow path act_type -> intensity_cmd;  
end lamppost_controller;

process implementation lamppost_controller.i
	subcomponents 
		color_thr : thread lamppost_controller_color_thr; 
		intensity_thr : thread lamppost_controller_intensity_thr;
	connections 
		c00 : port lp_ids -> color_thr.lp_ids;
		c01 : port act_type -> color_thr.act_type; 
		c02 : port color_thr.color_cmd -> color_cmd; 
		
		c10 : port lp_ids -> intensity_thr.lp_ids;
		c11 : port act_type -> intensity_thr.act_type;
		c12 : port intensity_thr.intensity_cmd -> intensity_cmd; 
		
	flows 
		f00 : flow path lp_ids -> c00 -> color_thr.f00 -> c02 -> color_cmd;
		f01 : flow path act_type -> c01 -> color_thr.f01 -> c02 -> color_cmd; 
		
		f10 : flow path lp_ids -> c10 -> intensity_thr.f10 -> c12 -> intensity_cmd;
		f11 : flow path act_type -> c11 -> intensity_thr.f11 -> c12 -> intensity_cmd;
		  
end lamppost_controller.i;

thread lamppost_controller_color_thr
	features
		lp_ids : in data port sslights::datatypes::lamppost.i;
		act_type : in data port sslights::datatypes::actuation_type;
		color_cmd : out data port sslights::datatypes::color_cmd;
	flows 
		f00 : flow path lp_ids -> color_cmd;
		f01 : flow path act_type -> color_cmd; 
	properties
		Dispatch_Protocol => Periodic;
		Period => 5ms;			
end lamppost_controller_color_thr;

thread lamppost_controller_intensity_thr
	features 
		lp_ids : in data port sslights::datatypes::lamppost.i;
		act_type : in data port sslights::datatypes::actuation_type;
	    intensity_cmd : out data port sslights::datatypes::intensity_cmd;
	flows 
		f10 : flow path lp_ids -> intensity_cmd; 
		f11 : flow path act_type -> intensity_cmd;
	properties
		Dispatch_Protocol => Periodic;
		Period => 5ms;		  
end lamppost_controller_intensity_thr;
end sslights::processing::lamppost_controller;